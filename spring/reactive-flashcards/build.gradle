plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.5'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'jacoco'
	id 'com.adarshr.test-logger' version '3.2.0'
}

group = 'me.dio.hiokdev'
version = '1.0.0'

java {
	sourceCompatibility = '21'
	targetCompatibility = '21'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	mapStructVersion = '1.5.5.Final'
	okhttp3Version = '4.12.0'
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'
	implementation 'org.springframework.boot:spring-boot-starter-mail'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

	implementation 'org.thymeleaf.extras:thymeleaf-extras-java8time:3.0.4.RELEASE'
	implementation "org.mapstruct:mapstruct:$mapStructVersion"
	implementation 'org.apache.commons:commons-lang3:3.14.0'
	implementation 'org.apache.commons:commons-collections4:4.4'
	implementation 'org.springdoc:springdoc-openapi-starter-webflux-ui:2.5.0'

	compileOnly 'org.projectlombok:lombok'

	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
	annotationProcessor "org.mapstruct:mapstruct-processor:$mapStructVersion"

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation ('com.github.javafaker:javafaker:1.0.2') { exclude module: 'snakeyaml' }
	testImplementation group: 'org.yaml', name: 'snakeyaml', version: '2.2'
	testImplementation 'org.projectlombok:lombok'
	testImplementation "com.squareup.okhttp3:okhttp:$okhttp3Version"
	testImplementation "com.squareup.okhttp3:mockwebserver:$okhttp3Version"
	testImplementation 'com.icegreen:greenmail:2.0.1'
	testImplementation 'commons-io:commons-io:2.16.1'

	testAnnotationProcessor 'org.projectlombok:lombok'
}

tasks.named('test') {
	jvmArgs '-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=*:5005'

	useJUnitPlatform()

	testlogger{
		theme 'mocha-parallel'
		showExceptions true
		slowThreshold 400
		showSummary true
		showPassed true
		showSkipped true
		showFailed true
		showStandardStreams true
		showSkippedStandardStreams true
		showPassedStandardStreams true
		showFailedStandardStreams true
	}

	finalizedBy jacocoTestReport
}

bootRun{
	jvmArgs '-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=*:5005'
}
